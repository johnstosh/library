plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.+'
    id 'io.spring.dependency-management' version '1.1.+'
}

group = 'com.muczynski'
version = '2.1.1'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.session:spring-session-jdbc'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'org.postgresql:postgresql'
    implementation 'com.google.cloud.sql:postgres-socket-factory:1.13.+'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.microsoft.playwright:playwright:1.+'
    testImplementation 'io.rest-assured:rest-assured:5.+'
    testImplementation 'io.rest-assured:spring-mock-mvc:5.+'

    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:junit-jupiter'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Marc4J for LOC call number lookup
    implementation 'org.marc4j:marc4j:2.9.5'

    // BYU Call Number library for sorting
    implementation 'edu.byu.hbll:callnumber:1.1.0'

    // iText 7 for PDF generation (library cards)
    implementation 'com.itextpdf:itext-core:8.0.5'

    // Metadata-extractor for reading EXIF orientation data
    implementation 'com.drewnoakes:metadata-extractor:2.19.0'
}

jar {
    enabled = false
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    useJUnitPlatform {
        // Exclude tests tagged with 'manual' by default
        // These are long-running tests that should only be run explicitly
        if (project.hasProperty('excludeTags')) {
            excludeTags project.property('excludeTags').split(',')
        }
    }
    // Increase heap for tests that handle large files (photo ZIP export/import)
    maxHeapSize = '2g'
    testLogging {
        // Only show detailed output for failed tests
        events 'failed'
        exceptionFormat 'full'
        showCauses true
        showStackTraces true
        // Optionally include standard streams (e.g., System.out) for failed tests
        showStandardStreams true
    }
}
